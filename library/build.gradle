plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src/" ]

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'flexbatch'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'FlexBatch'
                description = 'A library for batch drawing custom types of sprites or small meshes in LibGDX.'
                url = 'https://github.com/CypherCove/FlexBatch'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                organization {
                    name = 'Cypher Cove'
                    url = 'www.cyphercove.com'
                }
                developers {
                    developer {
                        id = 'cypher-cove'
                        name = 'Cypher Cove'
                        email = 'contact@cyphercove.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:cyphercove/flexbatch.git'
                    developerConnection = 'scm:git:git@cyphercove/flexbatch.git'
                    url = 'https://github.com/cyphercove/FlexBatch/'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

